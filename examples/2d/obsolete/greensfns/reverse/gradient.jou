# ----------------------------------------------------------------------
# Set vertex spacing with increasing spacing away from fault
# ----------------------------------------------------------------------
${bias_factor=1.05}

# Function for computing discretization size at end of curve with bias
${fun_dx='dxStart*bias_factor**ceil( ln(1-curveL/dxStart*(1-bias_factor))/ln(bias_factor))'}

# ----------------------------------------------------------------------
# Compute sizes at curve endpoints
# ----------------------------------------------------------------------

# dxA - size at vertex_xpos
${dxStart=dx}{curveL=Length(Id("ground_fw"))}{execute("dxA="//fun_dx)}

# dxB - size at vertex_xneg
${dxStart=dx}{curveL=Length(Id("ground_hw"))}{execute("dxB="//fun_dx)}

# dxC - size at vertex_fault_yneg, vertex_bot_yneg
${dxStart=dx}{curveL=Length(Id("fault_extended"))}{execute("dxC="//fun_dx)}

# dxD - size at vertex__xpos, vertex_top_xneg
${dxStart=dxA}{curveL=Length(Id("edge_xpos"))}{execute("dxD="//fun_dx)}

# ----------------------------------------------------------------------
# Reset sizes
# ----------------------------------------------------------------------
curve all scheme default
surface all sizing function none

# Set size on faults
curve fault_surface size {dx}

# Set bias on curves extending from faults
curve ground_fw scheme bias fine size {dx} factor {bias_factor} start vertex vertex_fault_top
curve ground_hw scheme bias fine size {dx} factor {bias_factor} start vertex vertex_fault_top
curve fault_extended scheme bias fine size {dx} factor {bias_factor} start vertex vertex_fault_bot

curve edge_yneg_hw size {dxC}

# A to D
curve edge_xpos scheme bias fine size {dxA} coarse size {dxD} start vertex vertex_top_xpos

# B to C
curve edge_xneg scheme bias fine size {dxA} coarse size {dxD} start vertex vertex_top_xneg

# D to C
curve edge_yneg_fw scheme bias fine size {dxA} coarse size {dxD} start vertex vertex_fault_yneg

surface all sizing function type bias start curve fault_surface factor {bias_factor}

# End of file
