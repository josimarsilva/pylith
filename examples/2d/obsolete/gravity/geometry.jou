# -*- Python -*- (syntax highlighting)
# ----------------------------------------------------------------------
#
# Brad T. Aagaard, U.S. Geological Survey
# Charles A. Williams, GNS Science
# Matthew G. Knepley, University of Chicago
#
# This code was developed as part of the Computational Infrastructure
# for Geodynamics (http://geodynamics.org).
#
# Copyright (c) 2010-2017 University of California, Davis
#
# See COPYING for license information.
#
# ----------------------------------------------------------------------
# WARNING: This file uses surface/curve ids from CUBIT 15.1. These may
# need to be updated for other versions of CUBIT and Trelis. Most of
# these are at the end of this file.
#
# ----------------------------------------------------------------------
# Set units to SI.
# ----------------------------------------------------------------------
${Units('si')}

# ----------------------------------------------------------------------
# Create surface using vertices
# ----------------------------------------------------------------------

# Block is 200 km x 100 km.
# -100 km <= x <= 100 km 
# -100 km <= y <= 0 m
reset
${faultX=10.0*km}
${domainW=200.0*km}
${domainH=100.0*km}
create vertex {-0.5*domainW} {-domainH}  0.0
${idVSbegin=Id("vertex")}
create vertex {-0.5*domainW} {-0.0*km}  0.0
create vertex {faultX} {0.0*km}  0.0
${idVT=Id("vertex")}
create vertex {+0.5*domainW} {0.0*km}  0.0
create vertex {+0.5*domainW} {-domainH}  0.0
${idVSend=Id("vertex")}
create surface vertex {idVSbegin} to {idVSend}

# ----------------------------------------------------------------------
# Create fault surfaces
# ----------------------------------------------------------------------
${faultDip=45.0}
${faultWidth=40.0*km}
create vertex {faultX-sind(faultDip)*domainH} {-domainH}  0.0
${idVF=Id("vertex")}
split surface 1 across location vertex {idVT} location vertex {idVF}

# ----------------------------------------------------------------------
# Create material interface surfaces
# ----------------------------------------------------------------------
${crustD=30.0*km}

# Constant thickness crust
create vertex {-0.5*domainW} {-crustD}  0.0
${idVM1=Id("vertex")}
create vertex {+0.5*domainW} {-crustD}  0.0
${idVM2=Id("vertex")}
split surface 2 across location vertex {idVM1} location vertex {idVM2}
split surface 3 across location vertex {idVM1} location vertex {idVM2}

# Split fault surface
create vertex {faultX-sind(faultDip)*faultWidth} {-cosd(faultDisp)*faultWidth}  0.0
${idVF=Id("vertex")}
# WARNING: Curve id may need updating for various versions of CUBIT/Trelis.
split curve 13  at vertex {idVF}  from vertex

# ----------------------------------------------------------------------
# Name geometric entities for use in meshing.
# ----------------------------------------------------------------------
surface 6 name "s_crust_xneg"
surface 4 name "s_crust_xpos"
surface 7 name "s_mantle_xneg"
surface 5 name "s_mantle_xpos"

curve 15 name "c_crust_xneg"
curve 16 name "c_mantle_xneg"
curve 11 name "c_crust_xpos"
curve 12 name "c_mantle_xpos"
curve 8 name "c_bottom_xneg"
curve 7 name "c_bottom_xpos"
curve 2 name "c_topo_xneg"
curve 3 name "c_topo_xpos"

curve 10 name "c_fault_crust"
curve 18 name "c_fault_mantle"
curve 17 name "c_fault_ext"

delete vertex all
